{-
  TASK 4 - Caesar Cipher
  Implement a Caesar cipher encryption and decryption functions.
  The input is a string (https://docs.ton.org/develop/smart-contracts/guidelines/internal-messages#simple-message-with-comment)
  where the text is encrypted in a cell (after 32bit 00000000 prefix flag), the algorithm rotates the characters and the last ASCII
  character should go to the beginning. Return new text encoded in cell.
-}

() recv_internal() {
}

;; builder _store_builder(builder to, builder what) asm(what to) "STB";

(builder) rec(int shift, slice string) {
  builder b = begin_cell();
  while (string.slice_bits()) {
    int ch = string~load_uint(8);
    if ((ch >= 97) & (ch <= 122)) {  ;; a-z
      ch = ((ch - 97 + shift) % 26) + 97;
    } elseif ((ch >= 65) & (ch <= 90)) {  ;; A-Z
      ch = ((ch - 65 + shift) % 26) + 65;
    }
    b = b.store_slice(
      begin_cell().store_uint(ch, 8).end_cell().begin_parse()
    );
  }

  if (string.slice_refs()) {
    builder res = rec(shift, string.preload_ref().begin_parse());
    b = b.store_ref(res.end_cell());
  }

  return b;
}

cell do_algo(int shift, cell text) {
  slice string = text.begin_parse();
  string~skip_bits(32);
  builder res = rec(shift, string);
  return begin_cell().store_uint(0, 32).store_builder(res).end_cell();
}

;; testable
(cell) caesar_cipher_encrypt(int shift, cell text) method_id {
  return do_algo(shift, text);
}

;; testable
(cell) caesar_cipher_decrypt(int shift, cell text) method_id {
  return do_algo(- shift, text);
}
