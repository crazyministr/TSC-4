{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}

{-
slice encode_number_to_text(int number) {
    builder text_b = begin_cell();
    if (number < 0) {
        text_b = text_b.store_slice("-");
        number = - number;
    }
    int len = 0;
    int value = 0;
    int mult = 1;
    do {
        (number, int res) = number.divmod(10);
        value = value + (res + 48) * mult;
        mult = mult * 256;
        len = len + 1;
    } until (number == 0);

    slice text = text_b.store_uint(value, len * 8).end_cell().begin_parse();
    return text;
}

;; To disable log - remove impure to all functions
;; To enable log - add impure to all functions
forall X -> X ~strdump_safe_not_pretty(slice value) impure asm "STRDUMP";
forall X -> X ~dump_safe(X value) impure asm "s0 DUMP";

_ ~strdump_safe(slice text) impure {
    slice text_extended = begin_cell()
            .store_slice("[ðŸŒŠ] ")
            .store_slice(text)
            .end_cell()
            .begin_parse();

    ~strdump_safe_not_pretty(text_extended);
}

_ ~strdump_value(slice text, var value) impure {
    slice text_extended = begin_cell().store_slice("[ðŸ­] ")
            .store_slice(text)
            .store_slice(": ")
            .end_cell()
            .begin_parse();
    ~strdump_safe_not_pretty(text_extended);
    ~dump_safe(value);
}

_ ~strdump_int(slice text, int value) impure {
    slice text_extended = begin_cell()
            .store_slice("[ðŸ‘€] ")
            .store_slice(text)
            .store_slice(": ")
            .store_slice(encode_number_to_text(value))
            .end_cell().begin_parse();
    ~strdump_safe_not_pretty(text_extended);
}
-}

() recv_internal() {
}

global tuple end_of_replace;

(int) ubitsize (int a) asm "UBITSIZE";
forall X -> int is_null?(X x) asm "ISNULL";
builder _store_builder_ref(builder to, builder what) asm(what to) "STBREF";

;; const empty_dict_value = "";
;; (cell) custom::udict_delete_get_max(cell dict, int key_len) asm(-> 0) "DICTUREMMAX";

(builder) rec(cell llist, int flag, int flag_len, int value, int i, int buffer, int buffer_len) {
  slice cs = llist.begin_parse();
  int j = i;

  repeat (cs.slice_bits()) {
    int bit = cs~load_uint(1);

    if (buffer_len == flag_len) {  ;; shift buffer
      int first_bit_pos = 1 << (buffer_len - 1);
      int first_bit = buffer & first_bit_pos;
      if (first_bit) {
        buffer ^= first_bit_pos;
      }
    } else {
      buffer_len += 1;
    }
    buffer = (buffer << 1) | bit;
    if (buffer == flag) {
      end_of_replace = cons(j, end_of_replace);
      (buffer, buffer_len) = (0, 0);
    }
    j += 1;
  }
  builder b = begin_cell();
  if (cs.slice_refs()) {
    builder inner_b = rec(cs.preload_ref(), flag, flag_len, value, j, buffer, buffer_len);
    b = b.store_builder(inner_b);
  }

  cs = llist.begin_parse();
  j -= 1;  ;; cuz added +1 above
  while (j >= i) {
    int end_pos = -1;
    if (~ end_of_replace.is_null?()) {
      end_pos = car(end_of_replace);
    }
    int start_pos = end_pos - flag_len + 1;
    if (j > end_pos) {
      int bits_to_read = min(cs.slice_bits(), j - end_pos);
      ;; ~strdump_int("bit_to_read", bits_to_read);
      slice to_insert = cs.slice_last(bits_to_read);
      cs~skip_last_bits(bits_to_read);
      if (b.builder_bits() + bits_to_read < 1024) {
        b = begin_cell().store_slice(to_insert).store_builder(b);
      } else {
        b = begin_cell().store_slice(to_insert)._store_builder_ref(b);
      }
      j -= bits_to_read;
    } elseif (j >= start_pos) {  ;; & (j <= end_pos)
      int bits_to_skip = min(cs.slice_bits(), j - start_pos + 1);
      cs~skip_last_bits(bits_to_skip);

      if (j == end_pos) {
        int value_bits = ubitsize(value);
        if (b.builder_bits() + value_bits < 1024) {
          b = begin_cell().store_uint(value, value_bits).store_builder(b);
        } else {
          b = begin_cell().store_uint(value, value_bits)._store_builder_ref(b);
        }

      }

      j -= bits_to_skip;
    } else {
      (_, tuple t) = uncons(end_of_replace);
      end_of_replace = t;
    }

  }

  return b;
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
  int flag_len = ubitsize(flag);
  end_of_replace = null();

  builder ans = rec(linked_list, flag, flag_len, value, 0, 0, 0);

  return ans.end_cell();
}
