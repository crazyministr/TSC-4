{-
  TASK 1 - Find branch of the cell tree
  Write the method that will find a branch of the tree by comparing its
  hash with the hash received in the first parameter. When the algorithm finds
  the subtree (branch) whose hash equals the received hash, the root cell of
  this branch should be returned. Return empty cell if the branch is not found.
-}

() recv_internal() {
}


(cell, int) rec(int hash, cell tree) {
  if (cell_hash(tree) == hash) {
    return (tree, -1);
  }
  slice node = tree.begin_parse();
  repeat (node.slice_refs()) {
    (cell found_cell, int found?) = rec(hash, node~load_ref());
    if (found?) {
      return (found_cell, found?);
    }
  }
  return (begin_cell().end_cell(), 0);
}

;; testable
(cell) find_branch_by_hash(int hash, cell tree) method_id {
  (cell res, _) = rec(hash, tree);
  return res;
}
